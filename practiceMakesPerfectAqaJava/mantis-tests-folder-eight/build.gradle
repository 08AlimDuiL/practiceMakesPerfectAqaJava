plugins {
    id 'java'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'neo'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.testng:testng:6.14.3'
    implementation 'org.seleniumhq.selenium:selenium-java:4.15.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    implementation 'commons-net:commons-net:3.9.0'
    implementation 'hu.blackbelt.bundles.subethasmtp:org.subethamail.subethasmtp:3.1.7_1'
    implementation 'ru.lanwen.verbalregex:java-verbal-expressions:1.8'
    implementation 'ch.qos.logback:logback-classic:1.5.20'


}

test {
    useTestNG() {
        suites('src/test/resources/testng.xml')
    }

    if (project.hasProperty('browser')) {
        systemProperty 'browser', "${browser}"
    }
    if (project.hasProperty('target')) {
        systemProperty 'target', "${target}"
    }
    if (project.hasProperty('verifyUi')) {
        systemProperty 'verifyUi', "${verifyUi}"
    }

    afterTest { desc, result ->
        println "┌─ Тест: ${desc.className}.${desc.name}"
        println "├─ Статус: ${result.resultType}"
        println "└─ Длительность: ${result.endTime - result.startTime}ms"
    }

    afterSuite { suite, result ->
        if (!suite.parent) {
            println "\n" + "╔══════════════════════════════════════════════════════╗"
            println "║             РЕЗУЛЬТАТЫ ТЕСТОВ                        ║"
            println "╠══════════════════════════════════════════════════════╣"
            println "║ Всего тестов: ${result.testCount}".padRight(55) + "║"
            println "║ Успешно: ${result.successfulTestCount}".padRight(55) + "║"
            println "║ Провалено: ${result.failedTestCount}".padRight(55) + "║"
            println "║ Пропущено: ${result.skippedTestCount}".padRight(55) + "║"
            println "║ Время: ${result.endTime - result.startTime}ms".padRight(55) + "║"
            println "╚══════════════════════════════════════════════════════╝"
        }
    }
}